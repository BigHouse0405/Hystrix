buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:3.0.3'
    }
}

plugins {
    id 'nebula.netflixoss' version '3.1.2'
	id 'me.champeau.gradle.jmh' version '0.2.0'
}

ext {
    githubProjectName = rootProject.name    
}

subprojects {
    apply plugin: 'nebula.netflixoss'
    apply plugin: 'java'
    apply plugin: 'nebula.provided-base'
    
    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    repositories {
        jcenter()
    }

    group = "com.netflix.${githubProjectName}"

    eclipse {
        classpath {
            // include 'provided' dependencies on the classpath
            plusConfigurations += [configurations.provided]
            downloadSources = true
            downloadJavadoc = true
        }
    }

    idea {
        module {
            // include 'provided' dependencies on the classpath
            scopes.COMPILE.plus += [configurations.provided]
        }
    }

	publishing {
		publications {
			nebula(MavenPublication) {
				pom.withXml {
					configurations.compile.resolvedConfiguration.firstLevelModuleDependencies.each { dep ->
						asNode().dependencies[0].dependency.find {
							it.artifactId[0].text() == dep.moduleName &&
							it.groupId[0].text() == dep.moduleGroup
						}.scope[0].value = 'compile'
					}
				}
			}
		}
	}
}
